import cd.go.plugin.buildsrc.license.NoticeFileGenerator
import cd.go.plugin.buildsrc.license.TeeRenderer
import com.github.jk1.license.render.SimpleHtmlReportRenderer

plugins {
    id "com.github.jk1.dependency-license-report"
}

group 'cd.go.plugin.config.yaml'
version "0.11.3"

apply plugin: 'java'
apply plugin: "com.github.jk1.dependency-license-report"

project.ext.pluginDesc = [
        version: project.version,
        goCdVersion: '18.12.0'
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.ant', name: 'ant', version: '1.10.7'
    compile group: 'com.beust', name: 'jcommander', version: '1.78'
    compile group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.13'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.25'

    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '2.1'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('*') {
            expand project.pluginDesc
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDir 'src/main/resources'
        }
    }

    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

test {
    useJUnit {
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut"
    }
}

// create a fat jar with all dependencies
jar {
    baseName = project.name
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    manifest {
        attributes 'Main-Class': 'cd.go.plugin.config.yaml.cli.YamlPluginCli'
    }

    from(generateLicenseReport) {
        into "dependency-license-report"
        exclude "NOTICE.txt"
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}

licenseReport {
    renderers = [new NoticeFileGenerator(new TeeRenderer(new SimpleHtmlReportRenderer()), "${project.buildDir}/reports/dependency-license/")]
}
